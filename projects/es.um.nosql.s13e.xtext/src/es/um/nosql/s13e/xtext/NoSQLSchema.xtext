grammar es.um.nosql.s13e.xtext.NoSQLSchema with org.eclipse.xtext.common.Terminals

import "http://www.modelum.es/NoSQLSchema" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

NoSQLSchema returns NoSQLSchema:
  {NoSQLSchema}
  'NoSQLSchema' name=EString
  '{'
    (entities+=EntityClass ( "," entities+=EntityClass)* )?
    (refClasses+=ReferenceClass ( "," refClasses+=ReferenceClass)* )?
  '}';

Classifier returns Classifier:
  EntityClass | ReferenceClass;

EntityClass returns EntityClass:
  (root?='root')? 'EntityClass' name=EString
  '{'
    ('parents' '{' parents+=[Classifier|EString] ( "," parents+=[Classifier|EString])* '}' )?
    variations+=StructuralVariation ( "," variations+=StructuralVariation)* 
  '}';

ReferenceClass returns ReferenceClass:
  'ReferenceClass' name=EString
  '{'
    ('parents' '{' parents+=[Classifier|EString] ( "," parents+=[Classifier|EString])* '}' )?
    variations+=StructuralVariation ( "," variations+=StructuralVariation)*
  '}';

StructuralVariation returns StructuralVariation:
  'Variation' variationId=EInt
  '{'
    ('count' count=ELong)?
    ('firstTimestamp' firstTimestamp=ELong)?
    ('lastTimestamp' lastTimestamp=ELong)?
    'properties' '{' properties+=Property ( "," properties+=Property)* '}' 
  '}';

Property returns Property:
  Attribute | Reference | Aggregate | Null;

Reference returns Reference:
  optional?='optional' name=EString ':' 'Reference' ('[' (lowerBound=EInt)?'..'(upperBound=EInt)? ']')?
    'references' refsTo=[EntityClass|EString]
  ('{'
    ('type' originalType=EString)?
    ('opposite' opposite=[Reference|EString])?
    ('features' features=[StructuralVariation|EString])?
  '}')?;

Aggregate returns Aggregate:
  optional?='optional' name=EString ':' 'Aggregate' ('[' (lowerBound=EInt)?'..'(upperBound=EInt)? ']')?
    'aggregates' '(' aggregates+=[StructuralVariation|EString] ( "," aggregates+=[StructuralVariation|EString])* ')' ;

Null returns Null:
  {Null}
  optional?='optional' name=EString ':' 'Null';

Attribute returns Attribute:
  optional?='optional' name=EString ':' type=Type;

Type returns Type:
  PrimitiveType | PList | PSet | PMap | PTuple;

PList returns PList:
  {PList}
  'PList' ('<' elementType=Type '>')?;

PrimitiveType returns PrimitiveType:
  {PrimitiveType}
  'PrimitiveType' name=EString;

PMap returns PMap:
  {PMap}
  'PMap' ('<' ('key' keyType=PrimitiveType)? ('value' valueType=Type)? '>')?;

PSet returns PSet:
  {PSet}
  'PSet' ('<' elementType=Type '>')?;

PTuple returns PTuple:
  {PTuple}
  'PTuple' ('<' elements+=Type ( "," elements+=Type)* '>' )?;

EString returns ecore::EString:
  STRING | ID | KEYWORD;

KEYWORD: 'NoSQLSchema' | 'root' | 'EntityClass' | 'ReferenceClass' | 'parents' | 'Variation'
  | 'count' | 'timestamp' | 'properties' | 'Reference' | 'references' | 'type' | 'opposite'
  | 'features' | 'Aggregate' | 'aggregates' | 'PList' | 'PrimitiveType' | 'PMap' | 'key'
  | 'value' | 'PSet' | 'PTuple' | 'optional';

EBoolean returns ecore::EBoolean:
  'true' | 'false';

EInt returns ecore::EInt:
  '-'? INT;

ELong returns ecore::ELong:
  '-'? INT;
