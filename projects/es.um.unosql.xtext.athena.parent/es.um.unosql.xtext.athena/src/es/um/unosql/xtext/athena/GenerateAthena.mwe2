module es.um.unosql.xtext.athena.GenerateAthena

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow
{
  component = XtextGenerator
  {
    configuration =
    {
      project = StandardProjectConfig
      {
        baseName = "es.um.unosql.xtext.athena"
        rootPath = rootPath
        runtimeTest = { enabled = false }
        eclipsePlugin = { enabled = true }
        eclipsePluginTest = { enabled = false }
        createEclipseMetaData = true
      }
      code =
      {
        encoding = "UTF-8"
        lineDelimiter = "\n"
        fileHeader = "/*\n * generated by Xtext \${version}\n */"
      }
    }
    language = StandardLanguage
    {
      name = "es.um.unosql.xtext.athena.Athena"
      fileExtensions = "athena"
      formatter = { generateStub = true }
      fragment = scoping.ImportNamespacesScopingFragment2 {}
      fragment = exporting.QualifiedNamesFragment2 {}
      fragment = builder.BuilderIntegrationFragment2 {}
      fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}
      junitSupport = { junitVersion = "5" }
      // generator = { generateXtendMain = true }
      parserGenerator = { options = { ignoreCase = true memoize = true /*backtrack=true*/} }
      serializer = { generateStub = false }
      validator =
      {
        composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
        composedCheck = "es.um.unosql.xtext.athena.validation.AthenaErrorValidator"
        composedCheck = "es.um.unosql.xtext.athena.validation.AthenaWarningValidator"
        generateDeprecationValidation = true
      }
    }
  }
}
