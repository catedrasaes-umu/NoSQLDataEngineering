module es.um.unosql.xtext.orion.GenerateOrion

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow
{
  component = XtextGenerator
  {
    configuration =
    {
      project = StandardProjectConfig
      {
        baseName = "es.um.unosql.xtext.orion"
        rootPath = rootPath
        runtimeTest = { enabled = false }
        eclipsePlugin = { enabled = true }
        eclipsePluginTest = { enabled = false }
        createEclipseMetaData = true
      }
      code =
      {
        encoding = "UTF-8"
        lineDelimiter = "\n"
        fileHeader = "/*\n * generated by Xtext \${version}\n */"
      }
    }
    language = StandardLanguage
    {
      name = "es.um.unosql.xtext.orion.Orion"
      fileExtensions = "orion"
      formatter = { generateStub = true }
      fragment = scoping.ImportNamespacesScopingFragment2 auto-inject {}
      junitSupport = { junitVersion = "5" }
      // generator = { generateXtendMain = true }
      parserGenerator = { options = { ignoreCase = true memoize = true /* backtrack = true */ } }
      referencedResource = "platform:/resource/es.um.unosql.xtext.athena/src/es/um/unosql/xtext/athena/Athena.xtext"
      referencedResource = "platform:/resource/es.um.unosql.xtext.athena/model/generated/Athena.genmodel"
      serializer = { generateStub = false }
      validator =
      {
        composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
        composedCheck = "es.um.unosql.xtext.orion.validation.OrionErrorValidator"
        // Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
        generateDeprecationValidation = true
      }
    }
  }
}
